plugins {
    id 'com.android.application'
}

android {
    compileSdk {
        version = release(36) {
            minorApiLevel = 1
        }
    }
    buildToolsVersion "36.1.0"

    defaultConfig {
        applicationId "io.github.lsposed.disableflagsecure"
        minSdkVersion 31
        targetSdkVersion 36
        versionCode rootProject.ext.commitCount
        versionName "4.3.0"
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
        externalNativeBuild {
            cmake {
                cppFlags "-DAPPLICATION_ID=\"${applicationId}\""
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '4.1.2+'
        }
    }

    Properties localProperties = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    }

    signingConfigs {
        if (localProperties.getProperty("storeFile") != null) {
            config {
                storeFile project.rootProject.file(localProperties.getProperty("storeFile"))
                storePassword localProperties.getProperty("storePassword")
                keyAlias localProperties.getProperty("keyAlias")
                keyPassword localProperties.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        configureEach {
            vcsInfo.include false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            if (localProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.config
            }
        }
        release {
            if (localProperties.getProperty("storeFile") != null) {
                signingConfig signingConfigs.config
            } else {
                signingConfig signingConfigs.debug
            }
            vcsInfo.include false
            minifyEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    lint {
        checkReleaseBuilds false
    }

    dependenciesInfo.includeInApk false
    namespace 'io.github.lsposed.disableflagsecure'
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.9.1'
    compileOnly 'io.github.libxposed:api:100'
    compileOnly project(":libxposed-compat")
}
